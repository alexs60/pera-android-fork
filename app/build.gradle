plugins {
    id "io.gitlab.arturbosch.detekt" version(libs.versions.detekt)
    id "com.android.application"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
    id "kotlin-android"
    id "kotlin-parcelize"
    id 'com.google.devtools.ksp'
    id "androidx.navigation.safeargs.kotlin"
    id "dagger.hilt.android.plugin"
    id 'com.google.firebase.firebase-perf'
}

apply from: 'https://raw.githubusercontent.com/Hipo/macaron/master/config/quality/quality.gradle'

def flavorsGradlePath = 'flavors.gradle'
def browsersGradlePath = 'browsers.gradle'
def offrampGradlePath = 'offramp.gradle'
if (project.file(flavorsGradlePath).exists()) {
    apply from: flavorsGradlePath
}
if (project.file(browsersGradlePath).exists()) {
    apply from: browsersGradlePath
}
if (project.file(offrampGradlePath).exists()) {
    apply from: offrampGradlePath
}

def getVersionName = {
    System.getenv("PERA_APP_VERSION_NAME")?.toString() ?: peraApplication.versionName
}

def getVersionCode = {
    System.getenv("PERA_APP_VERSION_CODE")?.toInteger() ?: peraApplication.versionCode
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion targets.compileSdkVersion
    defaultConfig {
        versionCode getVersionCode()
        versionName getVersionName()
        applicationId application.applicationId
        minSdkVersion targets.minSdkVersion
        targetSdkVersion targets.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        buildConfigField "String", "GitHash", "\"${getGitHash()}\""
        buildConfigField "String", "APPLICATION_NAME", '"pera"'
        buildConfigField "String", "DEEPLINK_PREFIX", '"algorand://"'
        buildConfigField "String", "VERSION_NAME", "\"$versionName\""
        buildConfigField "Integer", "VERSION_CODE", "$versionCode"
    }

    signingConfigs {
        releaseLocal {
            def localProps = new Properties()
            try {
                localProps.load(new FileInputStream(file('../local.properties')))
                storeFile file(localProps["RELEASE_STORE_FILE"])
                storePassword localProps["RELEASE_STORE_PASSWORD"]
                keyAlias localProps["RELEASE_KEY_ALIAS"]
                keyPassword localProps["RELEASE_KEY_PASSWORD"]
            } catch (e) {
                println(e)
            }
        }
    }

    buildTypes {
        release {
            // signingConfig signingConfigs.releaseLocal //enable this to sign the release
            multiDexEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [enableCrashReporting: "true", enableFirebasePerformanceLogcat: "false"]
        }

        debug {
            debuggable true
            applicationIdSuffix ".debug"
            multiDexEnabled true
            minifyEnabled false
            manifestPlaceholders = [enableCrashReporting: "false", enableFirebasePerformanceLogcat: "true"]
            resValue("string", "app_name", "Pera Wallet (Dev)")
        }
    }

    flavorDimensions "server"
    productFlavors {
        def apiKeyProps = new Properties()
        def apiUrlProps = new Properties()
        def arc59Props = new Properties()

        staging {
            dimension "server"
            applicationIdSuffix ".staging"

            apiKeyProps.load(new FileInputStream(file('../app/src/staging/api-key.properties')))
            apiUrlProps.load(new FileInputStream(file('../app/src/main/api-url.properties')))
            arc59Props.load(new FileInputStream(file('../app/arc59.properties')))

            buildConfigField "String", "ALGORAND_BASE_URL", apiUrlProps["NODE_TESTNET_URL"]
            buildConfigField "String", "ALGORAND_API_KEY", apiKeyProps["ALGORAND_API_KEY"]
            buildConfigField "String", "INDEXER_API_KEY", apiKeyProps["INDEXER_API_KEY"]
            buildConfigField "String", "MOBILE_API_KEY", apiKeyProps["MOBILE_API_KEY"]
            buildConfigField "String", "MOBILE_ALGORAND_MAINNET_BASE_URL", '"https://mainnet.staging.api.perawallet.app/"'
            buildConfigField "String", "MOBILE_ALGORAND_TESTNET_BASE_URL", '"https://testnet.staging.api.perawallet.app/"'
            buildConfigField "String", "PERA_3D_EXPLORER_BASE_URL", '"https://static-hosting.perawallet.app/3dviewer/index.html"'
            buildConfigField "String", "DISCOVER_WEBVIEW_USERNAME", apiKeyProps["DISCOVER_WEBVIEW_USERNAME"]
            buildConfigField "String", "DISCOVER_WEBVIEW_PASSWORD", apiKeyProps["DISCOVER_WEBVIEW_PASSWORD"]
            buildConfigField "String", "DISCOVER_VERSION", "\"${discoverVersions.staging}\""
            buildConfigField "String", "DISCOVER_URL", '"https://discover-mobile-staging.perawallet.app/"'
            buildConfigField "String", "DISCOVER_BROWSE_DAPP_URL", '"https://discover-mobile-staging.perawallet.app/main/browser"'
            buildConfigField "String", "CARDS_URL", '"https://cards-mobile-staging.perawallet.app"'
            buildConfigField "String", "MELD_URL", '"https://mainnet.staging.api.perawallet.app/v1/onramp-services/meld/redirect-to-fluidmoney/?walletAddress="'
            buildConfigField "String", "NODE_MAINNET_URL", apiUrlProps["NODE_MAINNET_URL"]
            buildConfigField "String", "NODE_TESTNET_URL", apiUrlProps["NODE_TESTNET_URL"]
            buildConfigField "String", "INDEXER_MAINNET_URL", apiUrlProps["INDEXER_MAINNET_URL"]
            buildConfigField "String", "INDEXER_TESTNET_URL", apiUrlProps["INDEXER_TESTNET_URL"]
            buildConfigField "String", "ARC59_APP_ADDR_TESTNET", arc59Props["ARC59_APP_ADDR_TESTNET"]
            buildConfigField "long", "ARC59_APP_ID_TESTNET", arc59Props["ARC59_APP_ID_TESTNET"]
            buildConfigField "String", "ARC59_APP_ADDR_MAINNET", arc59Props["ARC59_APP_ADDR_MAINNET"]
            buildConfigField "long", "ARC59_APP_ID_MAINNET", arc59Props["ARC59_APP_ID_MAINNET"]
        }

        prod {
            dimension "server"

            apiKeyProps.load(new FileInputStream(file('../app/src/prod/api-key.properties')))
            apiUrlProps.load(new FileInputStream(file('../app/src/main/api-url.properties')))
            arc59Props.load(new FileInputStream(file('../app/arc59.properties')))

            buildConfigField "String", "ALGORAND_BASE_URL", apiUrlProps["NODE_MAINNET_URL"]
            buildConfigField "String", "ALGORAND_API_KEY", apiKeyProps["ALGORAND_API_KEY"]
            buildConfigField "String", "INDEXER_API_KEY", apiKeyProps["INDEXER_API_KEY"]
            buildConfigField "String", "MOBILE_API_KEY", apiKeyProps["MOBILE_API_KEY"]
            buildConfigField "String", "MOBILE_ALGORAND_MAINNET_BASE_URL", '"https://mainnet.api.perawallet.app/"'
            buildConfigField "String", "MOBILE_ALGORAND_TESTNET_BASE_URL", '"https://testnet.api.perawallet.app/"'
            buildConfigField "String", "PERA_3D_EXPLORER_BASE_URL", '"https://static-hosting.perawallet.app/3dviewer/index.html"'
            buildConfigField "String", "DISCOVER_WEBVIEW_USERNAME", apiKeyProps["DISCOVER_WEBVIEW_USERNAME"]
            buildConfigField "String", "DISCOVER_WEBVIEW_PASSWORD", apiKeyProps["DISCOVER_WEBVIEW_PASSWORD"]
            buildConfigField "String", "DISCOVER_VERSION", "\"${discoverVersions.prod}\""
            buildConfigField "String", "DISCOVER_URL", '"https://discover-mobile.perawallet.app/"'
            buildConfigField "String", "DISCOVER_BROWSE_DAPP_URL", '"https://discover-mobile.perawallet.app/main/browser"'
            buildConfigField "String", "CARDS_URL", '"https://cards-mobile.perawallet.app"'
            buildConfigField "String", "MELD_URL", '"https://mainnet.api.perawallet.app/v1/onramp-services/meld/redirect-to-fluidmoney/?walletAddress="'
            buildConfigField "String", "NODE_MAINNET_URL", apiUrlProps["NODE_MAINNET_URL"]
            buildConfigField "String", "NODE_TESTNET_URL", apiUrlProps["NODE_TESTNET_URL"]
            buildConfigField "String", "INDEXER_MAINNET_URL", apiUrlProps["INDEXER_MAINNET_URL"]
            buildConfigField "String", "INDEXER_TESTNET_URL", apiUrlProps["INDEXER_TESTNET_URL"]
            buildConfigField "String", "ARC59_APP_ADDR_TESTNET", arc59Props["ARC59_APP_ADDR_TESTNET"]
            buildConfigField "long", "ARC59_APP_ID_TESTNET", arc59Props["ARC59_APP_ID_TESTNET"]
            buildConfigField "String", "ARC59_APP_ADDR_MAINNET", arc59Props["ARC59_APP_ADDR_MAINNET"]
            buildConfigField "long", "ARC59_APP_ID_MAINNET", arc59Props["ARC59_APP_ID_MAINNET"]
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        targetCompatibility = JavaVersion.VERSION_17
        sourceCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    buildFeatures {
        viewBinding true
        buildConfig = true
    }

    packagingOptions {
        exclude("lib/libnarcissus-macos-64.dylib")
        exclude("lib/libnarcissus-win-32.dll")
        exclude("lib/libnarcissus-win-64.dll")
    }

    configurations {
        all*.exclude module: 'bcprov-jdk15to18'
        all*.exclude module: 'bcutil-jdk18on'
        all*.exclude module: 'bcprov-jdk15on'
        all*.exclude module: 'bcutil-jdk15on'
    }

    namespace application.applicationId
}

dependencies {

    implementation project(':wallet-sdk')
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation libs.kotlinx.coroutines.core
    implementation libs.kotlinx.coroutines.android

    implementation libs.material
    implementation libs.flexbox
    implementation libs.appcompat
    implementation libs.legacy.support.v4
    implementation libs.lifecycle.extensions
    implementation libs.lifecycle.viewmodel.ktx
    implementation libs.lifecycle.common.java8
    implementation libs.constraintlayout
    implementation libs.core.ktx

    implementation platform(libs.firebase.bom)
    implementation libs.bundles.firebase

    implementation libs.biometric

    implementation libs.zxing.android.embedded

    implementation libs.room.runtime
    implementation libs.room.ktx
    ksp libs.room.compiler

    implementation libs.navigation.fragment.ktx
    implementation libs.navigation.ui.ktx

    implementation libs.ble

    implementation libs.fragment.ktx

    implementation libs.multidex

    implementation libs.browser

    implementation libs.paging.runtime.ktx

    implementation libs.tink.android

    implementation libs.review.ktx

    implementation libs.hilt.android.dagger
    ksp libs.hilt.android.compiler
    ksp libs.hilt.androidx
    implementation(platform(libs.koin.bom))
    implementation libs.koin.core
    implementation libs.koin.android

    implementation libs.glide.image
    ksp libs.glide.compiler

    implementation libs.lottie

    implementation libs.retrofit
    implementation libs.converter.gson
    implementation libs.logging.interceptor

    // Chart
    implementation libs.mpandroidchart

    coreLibraryDesugaring libs.desugar.jdk

    // Wallet Connect v1
    implementation libs.java.websocket
    implementation libs.khex
    implementation libs.moshi.core
    implementation libs.moshi.kotlin

    // Wallet Connect v2
    implementation(platform(libs.walletconnect))
    implementation(libs.walletconnect.android.core) {
        exclude group: 'com.jakewharton.timber', module: 'timber'
    }
    implementation libs.sign
    implementation libs.web3wallet

    // ExoPlayer
    implementation libs.exoplayer

    implementation(libs.algosdk) {
        exclude group: 'org.bouncycastle'
    }

    // Test
    testImplementation libs.mockk
    testImplementation libs.kotlinx.coroutines.test
    testImplementation libs.kotlin.test
    testImplementation libs.junit
    testImplementation libs.mockito
    androidTestImplementation libs.room.testing
    androidTestImplementation libs.runner
    androidTestImplementation libs.espresso.core
}
